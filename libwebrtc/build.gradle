apply plugin: 'com.android.library'

def useDefaultJavaCode = "true" == rootProject.findProperty("use_default_java_code")
def compileNativeCode = "true" == rootProject.findProperty("compile_native_code")
def webrtcRepo = rootProject.property("webrtc_repo")

android {
    compileSdkVersion 28

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets.main {
        if(useDefaultJavaCode)
        {
            java.srcDirs = [
                    "src/main/java/sdk/android/api",
                    "src/main/java/sdk/android/src/java",
                    "src/main/java/rtc_base/java/src",
                    "src/main/java/modules/audio_device/android/java/src",
            ]
        }
        else
        {
            java.srcDirs = [
                    "${webrtcRepo}/sdk/android/api",
                    "${webrtcRepo}/sdk/android/src/java",
                    "${webrtcRepo}/rtc_base/java/src",
                    "${webrtcRepo}/modules/audio_device/android/java/src",
            ]
        }


        if (!compileNativeCode) {
            jniLibs.srcDir 'libs'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        checkReleaseBuilds false
        // Or, if you prefer, you can continue to check for errors in release builds,
        // but continue the build even when errors are found:
        abortOnError false
    }

    if (compileNativeCode) {
        apply from: 'webrtc.gradle'
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation 'com.android.support:appcompat-v7:28.0.0'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}
